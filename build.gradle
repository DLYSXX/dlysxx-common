plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'cn.dlysxx.www.common'
archivesBaseName = "dlysxx-common"
version "${projectVersion}"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.slf4j:slf4j-api:${slf4jApiVersion}"
    implementation "commons-io:commons-io:${commonsIoVersion}"
    implementation "org.aspectj:aspectjweaver:${aspectjweaverVersion}"
    implementation "org.apache.logging.log4j:log4j-api:${log4jApiVersion}"
    implementation "org.apache.commons:commons-lang3:${commonsLang3Version}"
    implementation "org.springframework:spring-webmvc:${springWebMVCVersion}"
    implementation "org.springframework.boot:spring-boot:${springBootVersion}"
    implementation "org.apache.commons:commons-compress:${commonsCompressVersion}"
    implementation "org.springframework.boot:spring-boot-starter-aop:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-autoconfigure:${springBootVersion}"
    implementation "net.logstash.logback:logstash-logback-encoder:${logstashLogbackEncoderVersion}"
    annotationProcessor "org.springframework.boot:spring-boot-autoconfigure-processor:${springBootVersion}"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    testImplementation "ch.qos.logback:logback-classic:${logbackClassicVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
}

jar {
    enabled = true
}
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set("sources")
}
task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set("javadoc")
}
sourceSets.main.resources.srcDirs = ["src/main/java","src/main/resources"]

test {
    useJUnitPlatform()
}

signing {
    required { isReleaseVersion }
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            //if war then use components.web, if jar then use components.java
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'dlysxx-common'
                description = 'Dlysxx common utils'
                url = 'https://github.com/DLYSXX/dlysxx-common'
                licenses {
                    license {
                        name = 'BSD 3-Clause'
                        url = 'https://spdx.org/licenses/BSD-3-Clause.html'
                    }
                }
                developers {
                    developer {
                        name = 'shuai'
                        email = 'shuai.zhou@dlysxx.cn'
                    }
                }
                scm {
                    url = 'https://github.com/DLYSXX/dlysxx-common'
                    connection = 'scm:git:https://github.com/DLYSXX/dlysxx-common.git'
                    developerConnection = 'scm:git:https://github.com/DLYSXX/dlysxx-common.git'
                }
            }
        }
    }
    repositories {
        maven {
            name 'dlysxxCommonRepository'
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

javadoc {
    options.encoding = "UTF-8"
    failOnError = false
}
